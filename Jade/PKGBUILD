# Maintainer: Nima Fanniasl - NimaOS

pkgname=jade
_pkgname=crystal-jade
pkgver=1.3.5
pkgrel=1
pkgdesc="Scriptable backend & TUI Installer for Crystal Linux"
arch=('x86_64' 'aarch64')
license=('GPL3')
depends=('parted' 'arch-install-scripts')
makedepends=('cargo')
source=("git+https://github.com/NimaOS/jade.git")
sha256sums=('SKIP')

prepare() {
    cd "jade"
    cargo fetch --locked --target "${CARCH}-unknown-linux-gnu"
}

build() {
    cd "jade"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

package() {
    cd "jade"
    install -Dm 755 "target/release/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
}
